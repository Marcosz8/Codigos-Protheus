#include 'TOTVS.CH'
#include 'PROTHEUS.CH'
#include 'Topconn.ch'

    // =================================================================================================
    // ==========================  FUNÇÃO PRINCIPAL PROJETO TELA DE CADASTRO  ==========================
    // =================================================================================================
User Function Projeto01()

    //Variável da Tela Principal
    Private oDlg1
    Private cTituloJanela := "CADASTRO DE CLIENTES"

    // Variáveis do Folder/Pasta
    Private oFolder
    Private aFolder
    
    // =====================================================
    // ==== Variáveis do Folder/Pasta 01 Dados Pessoais ====
    // ===================================================== 

    Private oSayCod
    Private oGetCod
    Private cGetCod
   
    Private oSayNome
    Private oGetNome
    Private cGetNome

    Private oSayNomePo
    Private oGetNomePo
    Private cGetNomePo

    Private oSayNomeNa
    Private oGetNomeNa
    Private dGetNomeNa

    Private oSayNomeDdd
    Private oGetNomeDdd
    Private cGetNomeDdd

    Private oSayNomeCel
    Private oGetNomeCel
    Private cGetNomeCel

    Private oGroupSexo
    Private aSexo   := {"Masculino","Feminino"}
    Private nSexo   := 1
    Private oRadioSexo

    Private oSayCpf
    Private oGetCpf
    Private cGetCpf

    Private oSayRg
    Private oGetRg
    Private cGetRg

    Private oSayOrgao
    Private oGetOrgao
    Private cGetOrgao

    Private oSayUfRg
    Private aUfRg   := {"AC","AL","AP","AM","BA","CE","DF","ES","GO","MA","MT","MS","MG","PA","PB","PR","PE","PI","RJ","RN","RS","RO","RR","SC","SP","SE","TO"}
    Private cUfRg   := aUfRg[1]
    Private oComboUfRg

    Private oSayCnh
    Private oGetCnh
    Private cGetCnh
    
    Private oSayCatCnh
    Private aCatCnh := {"A","B","C","D","E","ACC","AB","AC","AD","AE"}
    Private cCatCnh := aCatCnh[1]
    Private oComboCatCnh

    Private oSayUfCnh
    Private aUfCnh  := {"AC","AL","AP","AM","BA","CE","DF","ES","GO","MA","MT","MS","MG","PA","PB","PR","PE","PI","RJ","RN","RS","RO","RR","SC","SP","SE","TO"}
    Private cUfCnh  := aUfCnh[1]
    Private oComboUfCnh

    Private oSayNac
    Private oGetNac
    Private cGetNac

    Private oSayEmail
    Private oGetEmail
    Private cGetEmail

    Private oGroupEstCi
    Private aEstCi  := {"Solteiro(a)","Casado(a)","Divorciado(a)","Viúvo(a)"}
    Private nEstCi  := 1
    Private oRadioEstCi

    // ================================================
    // ==== Variáveis do Folder/Pasta 02 Endereços ====
    // ================================================

    Private oSayEndereco
    Private oGetEndereco
    Private cGetEndereco
   
    Private oSayBairro
    Private oGetBairro
    Private cGetBairro

    Private oSayNum
    Private oGetNum
    Private cGetNum

    Private oSayComple
    Private oGetComple
    Private cGetComple

    Private oSayImovel
    Private aImovel := {"Casa","Apartamento","Sala Comercial","Galpão","Loja","Predio Comercial","Cháraca","Sitio","Fazenda","Outros"}
    Private cImovel := aImovel[1]
    Private oComboImovel

    Private oSayCep
    Private oGetCep
    Private cGetCep

    Private oSayUfEnd
    Private aUfEnd  := {"AC","AL","AP","AM","BA","CE","DF","ES","GO","MA","MT","MS","MG","PA","PB","PR","PE","PI","RJ","RN","RS","RO","RR","SC","SP","SE","TO"}
    Private cUfEnd  := aUfEnd[1]
    Private oComboUfEnd

    Private oSayCid
    Private oGetCid
    Private cGetCid

    // ===================================================
    // ==== Variáveis do Folder/Pasta 03 Escolaridade ====
    // ===================================================

    Private oSayEsco
    Private aEsco  := {"Não Alfabetizado","Alfabetizado","Fundamental Incompleto","Fundamental Completo","Médio Incompleto","Médio Completo", "Técnico Incompleto","Técnico Completo","Superior Incompleto","Superior Completo","Pós-graduação","MBA","Mestrado","Doutorado","Pós-Doutorado","Indefinido"}
    Private cEsco  := aEsco [1]
    Private oComboEsco

    Private oGroupLing

    Private oCheckCMais
    Private oCheckSql
    Private oCheckJava
    Private oCheckAdvpl
    Private oCheckPython
    Private oCheckJavaScript

    Private lCheckCMais      := .F.             
    Private lCheckSql        := .F.          
    Private lCheckJava       := .F.   
    Private lCheckAdvpl      := .F.   
    Private lCheckPython     := .F.   
    Private lCheckJavaScript := .F.

    Private oButtonMarkAll
    Private oButtonUnMarkAll    
      
    Private oGroupEstuda
    Private aEstuda  := {"Sim","Não"}
    Private nEstuda  := 2
    Private oRadioEstuda 

    Private oGroupLer
    Private aLer  := {"Sim","Não"}
    Private nLer  := 2
    Private oRadioLer 

    Private oSayArFor1
    Private oGetArFor1
    Private cGetArFor1

    Private oSayDatFor1
    Private oGetDatFor1
    Private dGetDatFor1

    Private oSayObs
    Private oTMultigetObs
    Private cObs
    
    Private oSayArFor2
    Private oGetArFor2
    Private cGetArFor2

    Private oSayDatFor2
    Private oGetDatFor2
    Private dGetDatFor2

    // ==========================================================
    // ==== Variáveis de elementos visuais da tela principal ====
    // ==========================================================
    
    Private oButtonAdd
    Private oButtonAlt
    Private oButtonCon
    Private oButtonExc

    // ==== Tela Principal ====
    oDlgTelaP           := TDialog():New(0,0,350,850,cTituloJanela,,,,,CLR_BLACK,CLR_WHITE,,,.T.)

    aFolder         := {"DADOS PESSOAIS","ENDEREÇO","ESCOLARIDADE"}
    oFolder         := TFolder():New(0,0,aFolder,aFolder,oDlgTelaP,,,, .T.,,430,140) 

    // ======================================================================================================================
    // ==========================  ELEMENTOS VISUAIS QUE COMPÕEM A FOLDER/PASTA 01 DADOS PESSOAIS  ==========================
    // ======================================================================================================================

    //==== Elementos Visuais Linha 01 ====
    oSayCod        := TSay():New(01,05,{||"Código"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetCod        := Space(6)
    oGetCod        := TGet():New(10,05,{|u| IIF(Pcount()>0,cGetCod:=u,cGetCod)},oFolder:aDialogs[1],025,009,"@ 999999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCod,,,, )
    oGetCod:cF3    := "SZ1"

    oSayNome        := TSay():New(01,40,{||"Nome"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetNome        := Space(30)
    oGetNome        := TGet():New(10,40,{|u| IIF(Pcount()>0,cGetNome:=u,cGetNome)},oFolder:aDialogs[1],090,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNome,,,, )

    oSayNomePo      := TSay():New(01,140,{||"Nome Profissional"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetNomePo      := Space(30)
    oGetNomePo      := TGet():New(10,140,{|u| IIF(Pcount()>0,cGetNomePo:=u,cGetNomePo)},oFolder:aDialogs[1],080,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNomePo,,,, )

    oSayNomeNa      := TSay():New(01,230,{||"Data de Nascimento"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    dGetNomeNa      := Date()
    oGetNomeNa      := TGet():New(10,230,{|u| IIF(Pcount()>0,dGetNomeNa:=u,dGetNomeNa)},oFolder:aDialogs[1],060,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,,,,,.T.)

    oSayNomeDdd     := TSay():New(01,300,{||"DDD Cel."},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetNomeDdd     := Space(4)
    oGetNomeDdd     := TGet():New(10,300,{|u| IIF(Pcount()>0,cGetNomeDdd:=u,cGetNomeDdd)},oFolder:aDialogs[1],015,009,"@E (99)",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNomeDdd,,,,)

    oGroupSexo      := tGroup():New(01,345,30,415,'Sexo',oFolder:aDialogs[1],,,.T.)
    oRadioSexo      := TRadMenu():New(010,350,aSexo,{|u|Iif (PCount()==0,nSexo,nSexo:=u)},oFolder:aDialogs[1],,,,,,,,60,20,,,,.T.)

    //==== Elementos Visuais Linha 02 ====

    oSayNomeCel     := TSay():New(30,05,{||"Celular"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetNomeCel     := Space(10)
    oGetNomeCel     := TGet():New(40,05,{|u| IIF(Pcount()>0,cGetNomeCel:=u,cGetNomeCel)},oFolder:aDialogs[1],040,009,"@E 99999-9999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNomeCel,,,,)

    oSayCpf         := TSay():New(30,55,{||"CPF"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetCpf         := Space(14)
    oGetCpf         := TGet():New(40,55,{|u| IIF(Pcount()>0,cGetCpf:=u,cGetCpf)},oFolder:aDialogs[1],045,009,"@E 999.999.999-99",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCpf,,,, )

    oSayRg          := TSay():New(30,110,{||"RG"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetRg          := Space(12)
    oGetRg          := TGet():New(40,110,{|u| IIF(Pcount()>0,cGetRg:=u,cGetRg)},oFolder:aDialogs[1],040,009,"@E 99.999.999-9",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetRg,,,, )

    oSayOrgao       := TSay():New(30,160,{||"Orgão Emissor"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetOrgao       := Space(12)
    oGetOrgao       := TGet():New(40,160,{|u| IIF(Pcount()>0,cGetOrgao:=u,cGetOrgao)},oFolder:aDialogs[1],045,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetOrgao,,,, )
    
    oSayUfRg        := TSay():New(30,215,{||"UF RG "},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    oComboUfRg      := tComboBox():New(40,215,{|u|if(PCount()>0,cUfRg:=u,cUfRg)},aUfRg,030,012,oFolder:aDialogs[1],,,,,,.T.,,,,,,,,,'cUfRg')

    oSayCnh         := TSay():New(30,255,{||"CNH"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetCnh         := Space(9)
    oGetCnh         := TGet():New(40,255,{|u| IIF(Pcount()>0,cGetCnh:=u,cGetCnh)},oFolder:aDialogs[1],040,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCnh,,,, )

    oSayCatCnh      := TSay():New(30,305,{||"Categ. CNH"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    oComboCatCnh    := tComboBox():New(40,305,{|u|if(PCount()>0,cCatCnh:=u,cCatCnh)},aCatCnh,030,012,oFolder:aDialogs[1],,,,,,.T.,,,,,,,,,'cCatCnh')

    //==== Elementos Visuais Linha 03 ====
    oSayUfCnh       := TSay():New(60,05,{||"UF CNH"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    oComboUfCnh     := tComboBox():New(70,05,{|u|if(PCount()>0,cUfCnh:=u,cUfCnh)},aUfCnh,030,012,oFolder:aDialogs[1],,,,,,.T.,,,,,,,,,'cUfCnh')

    oSayNac         := TSay():New(60,45,{||"Nacionalidade"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetNac         := Space(30)
    oGetNac         := TGet():New(70,45,{|u| IIF(Pcount()>0,cGetNac:=u,cGetNac)},oFolder:aDialogs[1],080,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNac,,,, )

    oSayEmail       := TSay():New(60,135,{||"Email"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetEmail       := Space(100)
    oGetEmail       := TGet():New(70,135,{|u| IIF(Pcount()>0,cGetEmail:=u,cGetEmail)},oFolder:aDialogs[1],175,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetEmail,,,, )

    oGroupEstCi     := tGroup():New(35,345,82,415,'Estado Civil',oFolder:aDialogs[1],,,.T.)
    oRadioEstCi     := TRadMenu():New(45,350,aEstCi,{|u|Iif (PCount()==0,nEstCi,nEstCi:=u)},oFolder:aDialogs[1],,,,,,,,60,20,,,,.T.)

    // =================================================================================================================
    // ==========================  ELEMENTOS VISUAIS QUE COMPÕEM A FOLDER/PASTA 02 ENDEREÇO   ==========================
    // =================================================================================================================   
    
    //==== Elementos Visuais Linha 01 ====
    oSayEndereco    := TSay():New(01,05,{||"Endereço"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetEndereco    := Space(30)
    oGetEndereco    := TGet():New(10,05,{|u| IIF(Pcount()>0,cGetEndereco:=u,cGetEndereco)},oFolder:aDialogs[2],095,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetEndereco,,,, )

    oSayBairro      := TSay():New(01,110,{||"Bairro"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetBairro      := Space(15)
    oGetBairro      := TGet():New(10,110,{|u| IIF(Pcount()>0,cGetBairro:=u,cGetBairro)},oFolder:aDialogs[2],060,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetBairro,,,, )

    oSayNum         := TSay():New(01,180,{||"Numero"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetNum         := Space(10)
    oGetNum         := TGet():New(10,180,{|u| IIF(Pcount()>0,cGetNum:=u,cGetNum)},oFolder:aDialogs[2],025,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNum,,,, )
   
    oSayComple      := TSay():New(01,215,{||"Complemento"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetComple      := Space(30)
    oGetComple      := TGet():New(10,215,{|u| IIF(Pcount()>0,cGetComple:=u,cGetComple)},oFolder:aDialogs[2],095,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetComple,,,, )
      
    oSayImovel      := TSay():New(01,320,{||"Tipo de Imóvel"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    oComboImovel    := tComboBox():New(10,320,{|u|if(PCount()>0,cImovel:=u,cImovel)},aImovel,060,012,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cImovel')

    //==== Elementos Visuais Linha 02 ====
    oSayCep         := TSay():New(30,05,{||"CEP"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetCep         := Space(8)
    oGetCep         := TGet():New(40,05,{|u| IIF(Pcount()>0,cGetCep:=u,cGetCep)},oFolder:aDialogs[2],030,009,"@E 99999-999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCep,,,, )
    
    oSayUfEnd       := TSay():New(30,55,{||"Estado"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    oComboUfEnd     := tComboBox():New(40,55,{|u|if(PCount()>0,cUfEnd:=u,cUfEnd)},aUfEnd,030,012,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cUfEnd')

    oSayCid         := TSay():New(30,95,{||"Cidade"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetCid         := Space(25)
    oGetCid         := TGet():New(40,95,{|u| IIF(Pcount()>0,cGetCid:=u,cGetCid)},oFolder:aDialogs[2],095,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCid,,,, )

    // =====================================================================================================================
    // ==========================  ELEMENTOS VISUAIS QUE COMPÕEM A FOLDER/PASTA 03 ESCOLARIDADE   ==========================
    // =====================================================================================================================   
    
    //==== Elementos Visuais Linha 01 ====
    oSayEsco        := TSay():New(01,05,{||"Escolaridade"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    oComboEsco      := tComboBox():New(10,05,{|u|if(PCount()>0,cEsco:=u,cEsco)},aEsco,090,012,oFolder:aDialogs[3],,,,,,.T.,,,,,,,,,'cEsco')

    oGroupLing      := tGroup():New(01,105,30,270,'Linguagens que Domina ?',oFolder:aDialogs[3],,,.T.)

    oCheckCMais     := TCheckBox():New(10,110,'C++',{|u| IIF(Pcount() > 0, lCheckCMais := u, lCheckCMais)},oFolder:aDialogs[3],100,210,,{|| lCheckCMais = !lCheckCMais},,,,,,.T.,,,)
    oCheckSql       := TCheckBox():New(10,140,'SQL',{|u| IIF(Pcount() > 0, lCheckSql := u, lCheckSql)},oFolder:aDialogs[3],100,210,,{|| lCheckSql = !lCheckSql},,,,,,.T.,,,)
    oCheckJava      := TCheckBox():New(10,180,'JAVA',{|u| IIF(Pcount() > 0, lCheckJava := u, lCheckJava)},oFolder:aDialogs[3],100,210,,{|| lCheckJava = !lCheckJava},,,,,,.T.,,,)
    oCheckAdvpl     := TCheckBox():New(20,110,'ADVPL',{|u| IIF(Pcount() > 0, lCheckAdvpl := u, lCheckAdvpl)},oFolder:aDialogs[3],100,210,,{|| lCheckAdvpl = !lCheckAdvpl},,,,,,.T.,,,)
    oCheckPython    := TCheckBox():New(20,140,'PYTHON',{|u| IIF(Pcount() > 0, lCheckPython := u, lCheckPython)},oFolder:aDialogs[3],100,210,,{|| lCheckPython = !lCheckPython},,,,,,.T.,,,)    
    oCheckJavaScript:= TCheckBox():New(20,180,'JAVASCRIPT',{|u| IIF(Pcount() > 0, lCheckJavaScript := u, lCheckJavaScript)},oFolder:aDialogs[3],100,210,,{|| lCheckJavaScript = !lCheckJavaScript},,,,,,.T.,,,)

    oButtonMarkAll  := TButton():New(08,230,"Todas",oFolder:aDialogs[3],{|| lCheckCMais := .T., lCheckSql := .T., lCheckJava := .T., lCheckAdvpl := .T., lCheckPython := .T., lCheckJavaScript := .T. }, 30,08,,,.F.,.T.,.F.,,.F.,,,.F. )
    oButtonUnMarkAll:= TButton():New(18,230,"Nenhuma",oFolder:aDialogs[3],{|| lCheckCMais := .F., lCheckSql := .F., lCheckJava := .F., lCheckAdvpl := .F., lCheckPython := .F., lCheckJavaScript := .F. }, 30,08,,,.F.,.T.,.F.,,.F.,,,.F. )

    oGroupEstuda    := tGroup():New(01,280,30,330,'Está Estudando?',oFolder:aDialogs[3],,,.T.)
    oRadioEstuda    := TRadMenu():New(10,290,aEstuda,{|u|Iif (PCount()==0,nEstuda,nEstuda:=u)},oFolder:aDialogs[3],,,,,,,,60,20,,,,.T.)

    oGroupLer       := tGroup():New(01,340,30,390,'Habito de Ler?',oFolder:aDialogs[3],,,.T.)
    oRadioLer       := TRadMenu():New(10,350,aLer,{|u|Iif (PCount()==0,nLer,nLer:=u)},oFolder:aDialogs[3],,,,,,,,60,20,,,,.T.)
    
    //==== Elementos Visuais Linha 02 ====

    oSayArFor1      := TSay():New(40,05,{||"Área de Formação 1"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetArFor1      := Space(25)
    oGetArFor1      := TGet():New(50,05,{|u| IIF(Pcount()>0,cGetArFor1:=u,cGetArFor1)},oFolder:aDialogs[3],070,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetArFor1,,,, )

    oSayDatFor1     := TSay():New(40,085,{||"Data Término"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    dGetDatFor1     := Date()
    oGetDatFor1     := TGet():New(50,085,{|u| IIF(Pcount()>0,dGetDatFor1:=u,dGetDatFor1)},oFolder:aDialogs[3],060,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,,,,,.T.)

    oSayObs         := TSay():New(40,150,{||"Observações"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cObs            := "Digite aqui informações adicionais"
    oTMultigetObs   := tMultiget():new(50 , 150, {| u | if( pCount() > 0, cObs := u, cObs ) }, oFolder:aDialogs[3], 240, 45, , , , , , .T. )
    
    //==== Elementos Visuais Linha 03 ====

    oSayArFor2      := TSay():New(70,05,{||"Área de Formação 2"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    cGetArFor2      := Space(25)
    oGetArFor2      := TGet():New(80,05,{|u| IIF(Pcount()>0,cGetArFor2:=u,cGetArFor2)},oFolder:aDialogs[3],070,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetArFor2,,,, )

    oSayDatFor2     := TSay():New(70,085,{||"Data Término"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,020)
    dGetDatFor2     := Date()
    oGetDatFor2     := TGet():New(80,085,{|u| IIF(Pcount()>0,dGetDatFor2:=u,dGetDatFor2)},oFolder:aDialogs[3],060,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,,,,,.T.)

    // ============================================================================================
    // ==========================  ELEMENTOS VISUAIS DA TELA PRINCIPAL   ==========================
    // ============================================================================================
    
    oButtonAdd      := TButton():New(150, 54, "INCLUIR",    oDlgTelaP,{|| AddSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )
    oButtonAlt      := TButton():New(150, 148, "ALTERAR",   oDlgTelaP,{|| AltSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )
    oButtonCon      := TButton():New(150, 242, "CONSULTAR", oDlgTelaP,{|| PesqSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )
    oButtonExc      := TButton():New(150, 336, "EXCLUIR",   oDlgTelaP,{|| DelSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )    
    
    // Ativa a janela principal    
    oDlgTelaP:Activate(,,,.T.)   

return

 // ============================================================
 // ==== FUNÇÃO ESTÁTICA 01 INCLUSÃO DE DADOS NA TABELA SZ1 ====
 // ============================================================

 
Static Function AddSZ1()

    DbSelectArea("SZ1")
    DBSetOrder(1)

    IF DbSeek(xFilial("SZ1")+cGetCod)

        Alert("O Código Digitado já existe, tente novamente com outro código")
    
    ELSE

        IF (!Empty(cGetCod) .AND. !Empty(cGetNome) .AND. !Empty(cGetNomeDdd) .AND. !Empty(cGetNomeCel) .AND. !Empty(cGetCpf) .AND. !Empty(cGetRg) .AND. !Empty(cGetOrgao) .AND. !Empty(cGetNac) .AND. !Empty(cGetEmail))
           
            DBSelectArea("SZ1")
            Reclock("SZ1",.T.) // Função que insere os dados na tabela SZ1
            
            // Informações Aba Dados Pessoais a serem gravadas nos campos da tabela SZ1
            SZ1 -> Z1_FILIAL  := xFilial("SZ1") // Preencherá o conteudo em branco, pois apesar de estarmos logados na filial 01 essa tabela é compartilhada
            SZ1 -> Z1_COD     := cGetCod   
            SZ1 -> Z1_NOME    := cGetNome
            SZ1 -> Z1_NOMPRO  := cGetNomePo                        
            SZ1 -> Z1_DATNAS  := dGetNomeNa 
            SZ1 -> Z1_DDDCEL  := StrTran(StrTran(cGetNomeDdd, "(",""),")","") // StrTran retira os caracteres das mascaras dos campos da tela antes de enviar para o banco de dados.                      
            SZ1 -> Z1_CEL     := StrTran(cGetNomeCel,"-","")                       
            SZ1 -> Z1_SEXO    := cValToChar(nSexo) // 1 para Masculino 2 para Feminino                     
            SZ1 -> Z1_CPF     := StrTran(StrTran(cGetCpf,".",""),"-","")                        
            SZ1 -> Z1_RG      := StrTran(StrTran(cGetRg,".",""),"-","")                       
            SZ1 -> Z1_ORGEMI  := cGetOrgao                        
            SZ1 -> Z1_UFRG    := cUfRg                             
            SZ1 -> Z1_CNH     := cGetCnh                           
            SZ1 -> Z1_CATCHN  := cCatCnh                           
            SZ1 -> Z1_UFCNH   := cUfCnh                            
            SZ1 -> Z1_ESTCIV  := cValToChar(nEstCi)                          
            SZ1 -> Z1_NACIO   := cGetNac               
            SZ1 -> Z1_EMAIL   := cGetEmail                 
            
            // Informações Aba Endereço a serem gravadas nos campos da tabela SZ1
            SZ1 -> Z1_ENDERE  := cGetEndereco                      
            SZ1 -> Z1_BAIRRO  := cGetBairro                        
            SZ1 -> Z1_NUMERO  := cGetNum                           
            SZ1 -> Z1_COMPLE  := cGetComple                       
            SZ1 -> Z1_TIOIMOV := cImovel                           
            SZ1 -> Z1_CEP     := StrTran(cGetCep,"-","")                            
            SZ1 -> Z1_ESTADO  := cUfEnd                            
            SZ1 -> Z1_CIDADE  := cGetCid

            //Informações Aba Escolaridade a serem gravadas nos campos da tabela SZ1
            SZ1 -> Z1_ESCOLAR := cEsco                         
            SZ1 -> Z1_LPCCC   := IIF(lCheckCMais,"1","2")      
            SZ1 -> Z1_LPSQL   := IIF(lCheckSql,"1","2")        
            SZ1 -> Z1_LPJAVA  := IIF(lCheckJava,"1","2")      
            SZ1 -> Z1_LPADVPL := IIF(lCheckAdvpl,"1","2")    
            SZ1 -> Z1_LPPYTHO := IIF(lCheckPython,"1","2")   
            SZ1 -> Z1_LPJAVSC := IIF(lCheckJavaScript,"1","2")
            SZ1 -> Z1_ESTUDAN := cValToChar(nEstuda)                  
            SZ1 -> Z1_LENDO   := cValToChar(nLer)                        
            SZ1 -> Z1_ARFOR1  := cGetArFor1                        
            SZ1 -> Z1_DATFOR1 := dGetDatFor1                 
            SZ1 -> Z1_OBS     := cObs 
            SZ1 -> Z1_ARFOR2  := cGetArFor2                        
            SZ1 -> Z1_DATFOR2 := dGetDatFor2 

            MsUnlock()  // Desbloqueia a area selecionada
            
            MsgInfo("Cliente incluido com sucesso!")

            LimpaCampos() // Limpa os campos da tela 
        ELSE
            Alert("Existem campos obrigatórios que não foram preenchidos! "+ Chr(13)+Chr(10) + Chr(13)+Chr(10) + "Verifique e tente novamente!")
        ENDIF

    ENDIF

    dbCloseArea("SZ1")

return

 // ============================================================
 // ==== FUNÇÃO ESTÁTICA 02 PESQUISA DE DADOS NA TABELA SZ1 ====
 // ============================================================
 
Static Function PesqSZ1()

    // Variáveis locais que salvam todas as áreas correntes
    Local aArea      := GetArea()
    Local aAreaSZ1   := SZ1->(GetArea())

    DbSelectArea("SZ1")
    DBSetOrder(1)
   
    IF DbSeek(xFilial("SZ1")+cGetCod)

        MsgInfo("Cliente encontrado com sucesso!")
            
        // Informações Aba Dados Pessoais a serem consultadas nos campos da tabela SZ1
        cGetNome        := SZ1 -> Z1_NOME 
        cGetNomePo      := SZ1 -> Z1_NOMPRO                         
        dGetNomeNa      := SZ1 -> Z1_DATNAS  
        cGetNomeDdd     := "("+AllTrim(SZ1 -> Z1_DDDCEL)+")"                       
        cGetNomeCel     := Substr(SZ1 -> Z1_CEL,1,5)+"-"+ Substr(SZ1 -> Z1_CEL,6,4)                           
        nSexo           := Val(SZ1 -> Z1_SEXO)                          
        cGetCpf         := Substr(SZ1 -> Z1_CPF,1,3)+"." + Substr(SZ1 -> Z1_CPF,4,3)+"." + Substr(SZ1 -> Z1_CPF,7,3)+"-" + Substr(SZ1 -> Z1_CPF,10,2) // Adciona caracteres por substrings                          
        cGetRg          := Substr(SZ1 -> Z1_RG,1,2)+"." + Substr(SZ1 -> Z1_RG,3,3)+"." + Substr(SZ1 -> Z1_RG,6,3)+"-"+ Substr(SZ1 -> Z1_RG,9,1)                       
        cGetOrgao       := SZ1 -> Z1_ORGEMI                         
        cUfRg           := SZ1 -> Z1_UFRG                                
        cGetCnh         := SZ1 -> Z1_CNH                            
        cCatCnh         := SZ1 -> Z1_CATCHN                            
        cUfCnh          := SZ1 -> Z1_UFCNH                             
        nEstCi          := Val(SZ1 -> Z1_ESTCIV)                           
        cGetNac         := SZ1 -> Z1_NACIO                
        cGetEmail       := SZ1 -> Z1_EMAIL                  
       
        // Informações Aba Endereço a serem consultadas no campos da tabela SZ1
        cGetEndereco    := SZ1 -> Z1_ENDERE                       
        cGetBairro      := SZ1 -> Z1_BAIRRO                         
        cGetNum         := SZ1 -> Z1_NUMERO                            
        cGetComple      := SZ1 -> Z1_COMPLE                        
        cImovel         := SZ1 -> Z1_TIOIMOV                             
        cGetCep         := Substr(SZ1 -> Z1_CEP,1,5)+"-"+ Substr(SZ1 -> Z1_CEP,6,3)                                 
        cUfEnd          := SZ1 -> Z1_ESTADO                             
        cGetCid         := SZ1 -> Z1_CIDADE 
        
        //Informações Aba Escolaridade a serem consultadas no campos da tabela SZ1
        cEsco           := SZ1 -> Z1_ESCOLAR                          
        lCheckCMais     := IIF(SZ1 -> Z1_LPCCC = "1",.T.,.F.)         
        lCheckSql       := IIF(SZ1 -> Z1_LPSQL = "1",.T.,.F.)           
        lCheckJava      := IIF(SZ1 -> Z1_LPJAVA = "1",.T.,.F.)         
        lCheckAdvpl     := IIF(SZ1 -> Z1_LPADVPL = "1",.T.,.F.)       
        lCheckPython    := IIF(SZ1 -> Z1_LPPYTHO = "1",.T.,.F.)    
        lCheckJavaScript:= IIF(SZ1 -> Z1_LPJAVSC = "1",.T.,.F.) 
        nEstuda         := Val(SZ1 -> Z1_ESTUDAN)                   
        nLer            := Val(SZ1 -> Z1_LENDO)                          
        cGetArFor1      := SZ1 -> Z1_ARFOR1                         
        dGetDatFor1     := SZ1 -> Z1_DATFOR1                  
        cObs            := SZ1 -> Z1_OBS  
        cGetArFor2      := SZ1 -> Z1_ARFOR2                         
        dGetDatFor2     := SZ1 -> Z1_DATFOR2 

        ELSE    
            Alert("Cliente não encontrado, tente novamente!")
                   
    ENDIF    

    dbCloseArea("SZ1")    

    // Restaura as áreas após uso
    RestArea(aAreaSZ1)
    RestArea(aArea) 

return

 // =============================================================
 // ==== FUNÇÃO ESTÁTICA 03 ALTERAÇÃO DE DADOS NA TABELA SZ1 ====
 // =============================================================
Static Function AltSZ1()
 
    DbSelectArea("SZ1")
    DBSetOrder(1)

    IF (Empty(cGetCod) .OR. Empty(cGetNome) .OR. Empty(cGetNomeDdd) .OR. Empty(cGetNomeCel) .OR. Empty(cGetCpf) .OR. Empty(cGetRg) .OR. Empty(cGetOrgao) .OR. Empty(cGetNac) .OR. Empty(cGetEmail))
        Alert("Existem campos obrigatórios que não foram preenchidos! "+ Chr(13)+Chr(10) + Chr(13)+Chr(10) + "Verifique e tente novamente!")
    ELSE

        IF DbSeek(xFilial("SZ1")+cGetCod)

            Reclock("SZ1",.F.) // Função que altera os dados na tabela SZ1
        
            // Informações Aba Dados Pessoais a serem alterados nos campos da tabela SZ1
            SZ1 -> Z1_FILIAL  := xFilial("SZ1") 
            SZ1 -> Z1_COD     := cGetCod   
            SZ1 -> Z1_NOME    := cGetNome
            SZ1 -> Z1_NOMPRO  := cGetNomePo                        
            SZ1 -> Z1_DATNAS  := dGetNomeNa 
            SZ1 -> Z1_DDDCEL  := StrTran(StrTran(cGetNomeDdd, "(",""),")","")                     
            SZ1 -> Z1_CEL     := StrTran(cGetNomeCel,"-","")                       
            SZ1 -> Z1_SEXO    := cValToChar(nSexo)                    
            SZ1 -> Z1_CPF     := StrTran(StrTran(cGetCpf,".",""),"-","")                        
            SZ1 -> Z1_RG      := StrTran(StrTran(cGetRg,".",""),"-","")                       
            SZ1 -> Z1_ORGEMI  := cGetOrgao                        
            SZ1 -> Z1_UFRG    := cUfRg                             
            SZ1 -> Z1_CNH     := cGetCnh                           
            SZ1 -> Z1_CATCHN  := cCatCnh                           
            SZ1 -> Z1_UFCNH   := cUfCnh                            
            SZ1 -> Z1_ESTCIV  := cValToChar(nEstCi)                          
            SZ1 -> Z1_NACIO   := cGetNac               
            SZ1 -> Z1_EMAIL   := cGetEmail                 
        
            // Informações Aba Endereço a serem alterados nos campos da tabela SZ1
            SZ1 -> Z1_ENDERE  := cGetEndereco                      
            SZ1 -> Z1_BAIRRO  := cGetBairro                        
            SZ1 -> Z1_NUMERO  := cGetNum                           
            SZ1 -> Z1_COMPLE  := cGetComple                       
            SZ1 -> Z1_TIOIMOV := cImovel                           
            SZ1 -> Z1_CEP     := StrTran(cGetCep,"-","")                            
            SZ1 -> Z1_ESTADO  := cUfEnd                            
            SZ1 -> Z1_CIDADE  := cGetCid
            
            //Informações Aba Escolaridade a serem alterados nos campos da tabela SZ1
            SZ1 -> Z1_ESCOLAR := cEsco                         
            SZ1 -> Z1_LPCCC   := IIF(lCheckCMais,"1","2")      
            SZ1 -> Z1_LPSQL   := IIF(lCheckSql,"1","2")        
            SZ1 -> Z1_LPJAVA  := IIF(lCheckJava,"1","2")      
            SZ1 -> Z1_LPADVPL := IIF(lCheckAdvpl,"1","2")    
            SZ1 -> Z1_LPPYTHO := IIF(lCheckPython,"1","2")   
            SZ1 -> Z1_LPJAVSC := IIF(lCheckJavaScript,"1","2")
            SZ1 -> Z1_ESTUDAN := cValToChar(nEstuda)                  
            SZ1 -> Z1_LENDO   := cValToChar(nLer)                        
            SZ1 -> Z1_ARFOR1  := cGetArFor1                        
            SZ1 -> Z1_DATFOR1 := dGetDatFor1                 
            SZ1 -> Z1_OBS     := cObs 
            SZ1 -> Z1_ARFOR2  := cGetArFor2                        
            SZ1 -> Z1_DATFOR2 := dGetDatFor2 

            MsUnlock()  
            MsgInfo("Cliente alterado com sucesso!")

        ENDIF

    ENDIF

    dbCloseArea("SZ1")
 
return

 // =============================================================
 // ==== FUNÇÃO ESTÁTICA 04 EXCLUSÃO DE DADOS NA TABELA SZ1 ====
 // =============================================================
Static Function DelSZ1()
    
    IF Msgyesno("Deseja excluir o cadastro do cliente: " + cGetNome +"?")

        DbSelectArea("SZ1")
        DBSetOrder(1)

        IF DbSeek(xFilial("SZ1")+cGetCod)

            Reclock("SZ1",.F.) // Bloqueia o registro para exclusão
            dbDelete()
            MsUnlock()
            MsgInfo("Cliente excluido com sucesso!")
            
        ENDIF

        dbCloseArea("SZ1")        
        LimpaCampos() // Limpa os campos da tela 

    ELSE

        Alert("Operação cancelada pelo usuário!")
    
    ENDIF

return

 // ==================================================
 // ==== FUNÇÃO ESTÁTICA 05 LIMPAR CAMPOS DA TELA ====
 // ==================================================
Static Function LimpaCampos()
    // Função atriubui aos campos os espaços ou as opções que eles tinham antes de serem preenchidos
    cGetCod         := Space(6)
    cGetNome        := Space(30)
    cGetNomePo      := Space(30)                      
    dGetNomeNa      := Date()  
    cGetNomeDdd     := Space(4)                      
    cGetNomeCel     := Space(10)                           
    nSexo           := 1                          
    cGetCpf         := Space(14)                         
    cGetRg          := Space(12)                      
    cGetOrgao       := Space(12)                  
    cUfRg           := aUfRg[1]                                
    cGetCnh         := Space(9)                          
    cCatCnh         := aCatCnh[1]                           
    cUfCnh          := aUfCnh[1]                             
    nEstCi          := 1                           
    cGetNac         := Space(30)               
    cGetEmail       := Space(100)                
    cGetEndereco    := Space(30)                      
    cGetBairro      := Space(15)                        
    cGetNum         := Space(10)                            
    cGetComple      := Space(30)                       
    cImovel         := aImovel[1]                             
    cGetCep         := Space(8)                                 
    cUfEnd          := aUfEnd[1]                             
    cGetCid         := Space(25)
    cEsco           := aEsco[1]                          
    lCheckCMais     := .F.         
    lCheckSql       := .F.           
    lCheckJava      := .F.         
    lCheckAdvpl     := .F.       
    lCheckPython    := .F.    
    lCheckJavaScript:= .F. 
    nEstuda         := 2                   
    nLer            := 2                          
    cGetArFor1      := Space(25)                       
    dGetDatFor1     := Date()                  
    cObs            := "Digite aqui informações adicionais"  
    cGetArFor2      := Space(25)                        
    dGetDatFor2     := Date() 

return
